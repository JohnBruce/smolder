#!/usr/bin/perl -w
use warnings;
use strict;
use Getopt::Long;
use Pod::Usage;
use File::Copy;


=head1 NAME

smolder_makedist - make a release of Smolder

=head1 SYNOPSIS

Build a distribution from the contents of the current directory.

  $ bin/smolder_makedist

=head1 DESCRIPTION

This script will create a distribution tar file for Smolder, using the
contents of a Smolder source tree.

=head2 What C<smolder_makedist> does

=over 4

=item 1

Extract version from Smolder.pm

=item 2

Look for data/build.db.  If found, use the information there to name
the output file for a binary distribution.  If not, build a source
distribution.

=item 3

Create list of files and directories to be excluded, F<bench/>,
F<conf/smolder.conf>, F<CVS/*>, F<.svn> and F<src/> in binary distributions.

=item 4

Creates INSTALL using pod2text on docs/ops_install.pod.

=item 5

Copies platform README from platform directory into the distribution
root, if present.

=item 6

Builds the tar.gz archive.

=back

=head1 OPTIONS

  --help      - Show usage
  --man       - Show full man page

=head1 SEE ALSO

Refer to F<docs/ops_release.pod> for information about building a new
release.


=cut



use File::Spec::Functions qw(catdir catfile splitdir rel2abs canonpath);
use FindBin qw($RealBin);
use Config;


# Set up SMOLDER_ROOT and PERL5LIB
BEGIN {
    # Find a SMOLDER_ROOT based on path to bin
    my @dir = splitdir(canonpath($RealBin));
    $ENV{SMOLDER_ROOT} ||= catdir(@dir[0 .. $#dir - 1]);

    # use $SMOLDER_ROOT/lib for modules
    my $lib    = catdir($ENV{SMOLDER_ROOT}, "lib");
    $ENV{PERL5LIB} = $ENV{PERL5LIB} ? "$ENV{PERL5LIB}:${lib}" :
      "${lib}";
    unshift @INC, $lib, "$lib/".$Config{archname};
}

use Smolder;
use Smolder::Platform;
use File::Find;
use File::Temp qw(tempdir);
use Cwd qw(cwd);

my ($help, $man);
pod2usage(2) unless
  GetOptions(help       => \$help,
             man        => \$man,
            );
pod2usage(1)             if $help;
pod2usage(-verbose => 2) if $man;
pod2usage("Unrecognized options to smolder_makedist: '".join(' ', @ARGV)."'\n")
  if @ARGV;

# make a temp directory to hold dist files
my $temp = tempdir(
    DIR     => catdir($ENV{SMOLDER_ROOT}, 'tmp'),
    CLEANUP => 1
);

# figure out name
my $name;
my $source_dist = 0;
my $platform;
if (-e catfile($ENV{SMOLDER_ROOT}, 'data', 'build.db')) {
    # get information for a binary distribution
    my %params = Smolder::Platform->build_params();
    $name = 'smolder-' . $Smolder::VERSION . '-' . $params{Platform} . 
      '-perl' . $params{Perl} . '-' . $params{Arch};
    $platform = $params{Platform};
} else {
    $source_dist = 1;
    $name = 'smolder-' . $Smolder::VERSION . '-src';
}

# make sure it's not already taken
die "$name.tar.gz already exists.\n" if -e "$name.tar.gz";

print "Building $name.\n";

my $old_dir = cwd;
chdir($temp) or die $!;

# make a link so tar will extract right
system('ln -s ' . $ENV{SMOLDER_ROOT} . " $name");
die "Unable to setup link." unless -e catfile($name, 'lib', 'Smolder.pm');

# build file list in temp dir
print "Computing exclude file list...\n";
my $list_file = catfile($temp, 'list.txt');
open(LIST, '>', $list_file) or die $!;

find({ no_chdir => 1,
       wanted => 
         sub { 
             # print "Considering $_...\n";
             return unless 
                       m!^$name/\./tmp/.!o or               # skip tmp contents
                       m!^$name/\./bench!o or               # skip bench
                       m!^$name/\./conf/smolder.conf$!o or  # skip smolder.conf
                       m!/CVS! or                           # skip all CVS dirs
                       m!\.cvsignore! or                    # skip all CVS dirs
                       m!/\.svn! or                         # skip all SVN dirs
                       m!~$! or                             # skip Emacs droppings
                       m!#! or                              # skip Emacs droppings
                       m!.swp! or                           # skip Vi droppings
                       m!.swo! or                           # skip Vi droppings
                       m!SMOLDER-[^/]+\.tar\.gz! or         # skip Smolder distribs
                       (not $source_dist and m!^$name/./src!o); 

             # print "Excluding $_.\n";
             print LIST "$_\n";
         } }, "$name/.");
close LIST;

# create INSTALL from docs/ops_install.pod
system("pod2text --loose $ENV{SMOLDER_ROOT}/docs/ops_install.pod > $ENV{SMOLDER_ROOT}/INSTALL");

# copy platform README if available
if ($platform and 
    -e "$ENV{SMOLDER_ROOT}/platform/$platform/README.$platform"){
    copy(
        "$ENV{SMOLDER_ROOT}/platform/$platform/README.$platform", 
        "$ENV{SMOLDER_ROOT}/README.$platform"
    );
} elsif (not $platform) {
    # TODO - replace with something that doesn't use 'cp'
    system("cp $ENV{SMOLDER_ROOT}/platform/*/README.* $ENV{SMOLDER_ROOT}/");
}

# create tar file using exclude list
print "Creating tar file '$name.tar.gz'...\n";
my $dist_file = catfile($old_dir, $name . '.tar.gz');
system("tar czf $dist_file -X $list_file $name/./*") == 0
  or die "tar failed: $?";

# remove INSTALL and platform README
unlink("$ENV{SMOLDER_ROOT}/INSTALL") or die $!;
unlink("$ENV{SMOLDER_ROOT}/README.$platform") 
  if $platform and -e "$ENV{SMOLDER_ROOT}/README.$platform";
unlink("rm $ENV{SMOLDER_ROOT}/README.*") 
  if not $platform;

# remove link
unlink($name) or die $!;

print "\n\nDistribution complete in $name.tar.gz\n";
