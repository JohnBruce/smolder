#!/usr/bin/env perl

use strict;
use warnings;

=pod

=head1 NAME

smolder_test

=head1 SYNOPSIS

    ./bin/smolder_test 

    ./bin/smolder_test --verbose

    ./bin/smolder_test --test_files t/

    ./bin/smolder_test --xml > report.xml

=head1 DESCRIPTION

Script used to run the Smolder tests and output either to the screen using the
standard L<Test::Harness> format or create an HTML report of the tests.

=cut

use Config;
use Getopt::Long;
use FindBin qw($RealBin);
use File::Spec::Functions qw(catdir catfile splitdir);

BEGIN {

    # Find a SMOLDER_ROOT based on path to bin
    my @dir = splitdir($RealBin);

    #remove any blank elements on the end of the @dir
    pop(@dir) while ( !$dir[$#dir] );
    $ENV{SMOLDER_ROOT} ||= catdir( @dir[ 0 .. $#dir - 1 ] );

    # use $SMOLDER_ROOT/lib for modules
    my $lib = catdir( $ENV{SMOLDER_ROOT}, "lib" );
    $ENV{PERL5LIB} =
      $ENV{PERL5LIB}
      ? "$ENV{PERL5LIB}:${lib}"
      : "${lib}";
    unshift @INC, $lib, "$lib/" . $Config{archname};

    eval { require Smolder::Conf };
    warn << "END" and exit(1) if $@;
######################################################################

Cannot load Smolder.

Error message:

$@

######################################################################
END

    unshift( @INC,
        catfile( $FindBin::Bin, '..', 'lib' ),
        catfile( $FindBin::Bin, '..', 'lib', $Config{archname} ) );
}

use Smolder::Script;
use Smolder::Conf qw(InstallRoot);
use Test::Harness;
use Test::TAP::XML;
use IO::Scalar;
use Pod::Usage;
use File::Find;

$ENV{HARNESS_ACTIVE} = 1;

=head1 OPTIONS

=over

=item verbose

The output of the test should be verbose. This is different
from the C< quiet > option since it just affects how L<Test::Harness>
runs the tests. By default this option is false.

    ./bin/smolder_test --verbose

=item quiet

Surpress any normal output from the different scripts that it
uses, or any normal output it may emit itself. This will not silence fatal errors. 
By default this option is false.

    ./bin/smolder_test --quiet

The user used when performing a C<freshsvn>.
This is used in combination with the C<svnuser> option.
By default, this option is an empty string.

=item xml

This options tess the script to create an XML structure of the test ouput
instead of the normal L<Test::Harness> style report. It will be output to the file
F<smolder_test.xml>.

=item files

This is a comma-delimited list of files (relative to the B<InstallRoot> root) that will be tested.
By default this will be every file in the t/ dir (and all subdirs or t/) that has a '.t' 
extension. 

    ./bin/smolder_test --files 't/control/*'

    ./bin/smolder_test --files t/control/login.t

=back

=cut

# default options
my $verbose   = 0;
my $quiet     = 0;
my $xml       = 0;
my @testfiles = ();
my $help      = 0;
my $man       = 0;

GetOptions(
    'verbose!'     => \$verbose,
    'quiet!'       => \$quiet,
    'files=s'      => \@testfiles,
    'testfiles=s'  => \@testfiles,
    'test_files=s' => \@testfiles,
    'help!'        => \$help,
    'man!'         => \$man,
    'xml'          => \$xml,
  )
  || pod2usage();

if ($help) {
    pod2usage(
        -exitval => 0,
        -verbose => 1,
    );
} elsif ($man) {
    pod2usage(
        -exitval => 0,
        -verbose => 2,
    );
}

my $junk     = '';
my $junk_err = '';

# if we want to be quite then surpress printing to STDOUT
if ($quiet) {
    tie( *STDOUT, 'IO::Scalar', \$junk );
    tie( *STDERR, 'IO::Scalar', \$junk_err );
}

my $root = InstallRoot();

# go the the Smolder root
print_header("Changing into Smolder Root $root");
chdir($root)
  or die "Could not change into $root directory! $!";

# now run the tests
print_header("Running Tests");

# if we don't have anything in tests
if ( !scalar(@testfiles) ) {

    # do a depth-first search on the 't' directory to find all of the files
    find(
        sub {
            if ( $_ =~ /\.t$/ ) {
                push( @testfiles, $File::Find::name );
            }
        },
        ,
        't',
    );
} else {

    # split on commas
    @testfiles = split( /,/, join( ',', @testfiles ) );

    # now expand any globs
    @testfiles = map { glob($_) } @testfiles;
}

# if we have aaa_dbcount and zzz_dbcount in the list, shuffle them to the
# front and back respectively
my ($file) = grep( /aaa_dbcount/, @testfiles );
@testfiles = grep( !/aaa_dbcount/, @testfiles );
unshift @testfiles, $file if $file;

($file) = grep( /zzz_dbcount/, @testfiles );
@testfiles = grep( !/zzz_dbcount/, @testfiles );
push @testfiles, $file if $file;

# if we want XML output
if ($xml) {
    my $test_model = Test::TAP::XML->new();
    $test_model->run_tests(@testfiles);
    print $test_model->xml();

    # else we just want to run normal Test::Harness tests
} else {
    $Test::Harness::Verbose = $verbose;
    eval { runtests(@testfiles) };
}

if ($quiet) {
    untie(*STDOUT);
    untie(*STDERR);
}

sub print_header {
    my $header = shift;

    if ( !$quiet && $verbose ) {

        # if we have the modules available
        print "\n";
        print '#' x 60;
        print "\n#    $header    \n";
        print '#' x 60;
        print "\n\n";
    }
}

